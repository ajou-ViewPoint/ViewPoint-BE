plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.www'
version = '0.0.1-SNAPSHOT'
description = 'viewpoint'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'
    implementation 'com.mysql:mysql-connector-j'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation("org.locationtech.jts:jts-core:1.19.0")
    implementation("org.springframework.boot:spring-boot-starter-webflux")

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'org.seleniumhq.selenium:selenium-java:4.25.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.9.2'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // 테스트 후 자동으로 리포트 생성
}

// JaCoCo 설정
jacoco {
    toolVersion = "0.8.11"
}

// JaCoCo 테스트 리포트 설정
jacocoTestReport {
    dependsOn test // 테스트 실행 후 리포트 생성

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    // 제외할 클래스 패턴 (DTO, Entity, Config 등)
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/model/dto/**',
                    '**/model/entity/**',
                    '**/model/request/**',
                    '**/model/response/**',
                    '**/model/enums/**',
                    '**/config/**',
                    '**/exception/**',
                    '**/*Application*',
                    '**/dto/**'
            ])
        }))
    }
}

// JaCoCo 커버리지 검증 규칙 (선택 사항)
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0 // 최소 커버리지 비율 (0% - 필요시 조정)
            }

            // 제외할 클래스
            excludes = [
                    '*.model.dto.*',
                    '*.model.entity.*',
                    '*.model.request.*',
                    '*.model.response.*',
                    '*.model.enums.*',
                    '*.config.*',
                    '*.exception.*',
                    '*.*Application*',
                    '*.dto.*'
            ]
        }
    }
}
